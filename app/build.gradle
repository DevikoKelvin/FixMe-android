plugins {
    alias(libs.plugins.android.application)
    alias(libs.plugins.kotlin.android)
    id 'kotlin-parcelize'
}

def getVerCode(boolean increment = false) {
    def propFile = file("../buildNumber.properties")
    def properties = new Properties()
    properties.load(new FileInputStream(propFile))
    def versionCode = properties['VERSION_CODE'].toInteger()

    if (increment) {
        properties['VERSION_CODE'] = (versionCode + 1).toString()
        properties.store(propFile.newWriter(), null)
    }

    return versionCode
}

android {
    namespace 'com.erela.fixme'
    compileSdk = 36

    defaultConfig {
        applicationId "com.erela.fixme"
        minSdk 26
        //noinspection EditedTargetSdkVersion
        targetSdk 36

        versionCode getVerCode(!project.gradle.startParameter.taskNames.any { it.contains('assemble') || it.contains('bundle') })
        versionName "1.3.3"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        renderscriptSupportModeEnabled true

        ndk {
            abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86_64'
        }
    }

    signingConfigs {
        release {
            storeFile file('D:\\ERELA_Projects\\Android_Mobile_Apps\\FixMe-android\\keystore.jks')
            storePassword "erela_fixme"
            keyAlias "fixme_erela"
            keyPassword "erela_fixme"
        }
    }

    buildTypes {
        release {
            buildConfigField 'String', 'BASE_URL', "\"https://182.23.21.202:8282/fixme/\""
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            buildConfigField 'String', 'BASE_URL', "\"https://182.23.21.202:8282/fixme/\""
            minifyEnabled false
            shrinkResources false
        }
    }

    applicationVariants.configureEach { variant ->
        variant.outputs.configureEach {
            outputFileName = "Erela_FixMe_prerelease_v${variant.versionName}.apk"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = '11'
    }

    buildFeatures {
        viewBinding true
        buildConfig true
    }
}

dependencies {
    // Core
    implementation libs.androidx.core.ktx

    // UI
    implementation libs.androidx.appcompat
    implementation libs.material
    implementation libs.design
    implementation libs.androidx.activity
    implementation libs.androidx.constraintlayout
    implementation libs.swipeRefreshLayout

    // 3rd-party
    implementation libs.flexbox
    implementation libs.lottie
    implementation libs.glide
    implementation libs.circleIndicator
    implementation libs.retrofit
    implementation libs.okhttp3
    implementation libs.converter.gson
    implementation libs.logging.interceptor
    implementation libs.appxupdater
    implementation libs.pusher.java.client
    implementation libs.balloon
    implementation libs.shimmer

    // Testing
    testImplementation libs.junit
    androidTestImplementation libs.androidx.junit
    androidTestImplementation libs.androidx.espresso.core
}